plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'checkstyle'
    id 'org.owasp.dependencycheck' version '9.1.0'
    id 'com.github.ben-manes.versions' version "0.51.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
}

group = 'es.in2'
version = '0.6.0'

java {
    sourceCompatibility = '17'
}

sonar {
    properties {
        property "sonar.projectName", "Desmos API"
        property "sonar.projectKey", "in2workspace_desmos"
        property "sonar.organization", "in2workspace"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions",
                "src/main/java/es/in2/desmos/DesmosApiApplication.java, " +
                        "src/main/java/es/in2/desmos/infrastructure/configs/properties/**, " +
                        "src/main/java/es/in2/desmos/infrastructure/configs/FlywayConfig.java, " +
                        "src/main/java/es/in2/desmos/infrastructure/configs/QueueConfig.java"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.configureEach {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group.startsWith('com.fasterxml.jackson')) {
                details.useVersion '2.17.0'
            }
        }
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0'

    // Data
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.postgresql:postgresql:42.7.3'
    runtimeOnly 'org.postgresql:r2dbc-postgresql:1.0.5.RELEASE'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    // Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.5.0'

    // Logback
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    // Metrics & Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.12.6'

    // Resilience
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects:6.1.8'
    implementation 'org.aspectj:aspectjrt:1.9.22'
    implementation 'org.aspectj:aspectjweaver:1.9.22'

    // Unit Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test:3.6.5'
    testImplementation 'org.testng:testng:7.10.2'

    // Integration Test
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    testImplementation 'org.testcontainers:r2dbc:1.19.8'
    testImplementation 'org.testcontainers:postgresql:1.19.8'
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}

tasks.register('printVersion') {
    doLast {
        println version
    }
}

tasks.register('printProjectName') {
    doLast {
        println rootProject.name
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/DesmosApiApplication.class',
                    '**/infrastructure/configs/properties/**',
                    '**/infrastructure/configs/FlywayConfig.class',
                    '**/infrastructure/configs/QueueConfig.class'
            ])
        }))
    }
}

checkstyle {
    toolVersion = '10.16.0'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

tasks.register('checkstyle') {
    reports {
        xml.required = false
        html.required = true
    }
}

// Dependency Maintenance
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
